/*
 * (c) Copyright 2018 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
        gradlePluginPortal() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    dependencies {
        classpath 'com.palantir.jakartapackagealignment:jakarta-package-alignment:0.5.0'
        classpath 'com.palantir.gradle.jdks:gradle-jdks:0.31.0'
        classpath 'com.palantir.gradle.jdkslatest:gradle-jdks-latest:0.7.0'
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.12.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:4.192.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.12.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:2.0.0'
        classpath 'com.palantir.gradle.revapi:gradle-revapi:1.7.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.28.0'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.7.0'
    }
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.revapi'
apply plugin: 'com.palantir.baseline-java-versions'
apply plugin: 'com.palantir.jdks.latest'

allprojects {
    apply plugin: 'com.palantir.java-format'
    apply plugin: 'com.palantir.jakarta-package-alignment'
    group 'com.palantir.refreshable'
    version System.env.CIRCLE_TAG ?: gitVersion()

    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }
}

subprojects {
    apply plugin: 'com.palantir.java-format'
    apply plugin: 'java-library'
    apply plugin: 'org.inferred.processors'

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Xlint:deprecation', '-Werror']
    }

    plugins.withId('com.palantir.baseline-error-prone', {
        dependencies {
            errorprone 'com.uber.nullaway:nullaway'
        }
        tasks.withType(JavaCompile).configureEach {
            options.errorprone.option('NullAway:AnnotatedPackages', 'com.palantir')
        }
    })
}

javaVersions {
    libraryTarget = 11
    runtime = 17
}
